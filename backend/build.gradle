plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.3'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'feedzupzup'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {

    //develop tool
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.9'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    //aws
    implementation 'software.amazon.awssdk:s3:2.30.16'

    //db
    runtimeOnly 'com.mysql:mysql-connector-j'
    implementation 'org.flywaydb:flyway-core'
    implementation "org.flywaydb:flyway-mysql"

    //test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:mysql'

    testImplementation 'io.rest-assured:rest-assured:5.5.0'
}

tasks.register('copySecret', Copy) {
    from file('./backend-submodule/main')
    include 'application*.yml'
    into 'src/main/resources'
}

tasks.register('copySecretTest', Copy) {
    from file('./backend-submodule/test')
    include 'application*.yml'
    into 'src/test/resources'
}

tasks.register('copySecretAlloy', Copy) {
    from file('./backend-submodule/alloy')
    include 'config.alloy'
    into '.'
}

compileJava.dependsOn copySecret, copySecretTest, copySecretAlloy
processResources.dependsOn copySecret, copySecretAlloy
processTestResources.dependsOn copySecretTest

test {
    useJUnitPlatform()
}
