plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.3'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'feedzupzup'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {

    //develop tool
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.9'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    //security
    implementation 'org.springframework.security:spring-security-crypto'

    //session
    implementation 'org.springframework.session:spring-session-jdbc'

    //aws
    implementation 'software.amazon.awssdk:s3:2.30.16'

    //QR
    implementation 'com.google.zxing:core:3.5.2'
    implementation 'com.google.zxing:javase:3.5.2'

    //db
    runtimeOnly 'com.mysql:mysql-connector-j'
    implementation 'org.flywaydb:flyway-core'
    implementation "org.flywaydb:flyway-mysql"

    //cache
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'com.github.ben-manes.caffeine:caffeine'

    //metric
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus'

    //log
    implementation 'net.logstash.logback:logstash-logback-encoder:7.4'

    //test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:mysql'
    testImplementation 'org.testcontainers:localstack'

    testImplementation 'io.rest-assured:rest-assured:5.5.0'

    //firebase
    implementation 'com.google.firebase:firebase-admin:9.5.0'

    //profanity filter
    implementation 'io.github.vaneproject:vane-badwordfiltering:1.0.0'
}

tasks.register('copySecret', Copy) {
    mustRunAfter copySecretAlloy
    from file('./backend-submodule/main')
    include 'application*.yml'
    into 'src/main/resources'
}

tasks.register('copySecretTest', Copy) {
    mustRunAfter copySecretAlloy
    from file('./backend-submodule/test')
    include 'application*.yml'
    into 'src/test/resources'
}

tasks.register('copySecretAlloy', Copy) {
    from file('./backend-submodule/alloy')
    include 'config.alloy'
    into '.'
}

tasks.register('copyFirebaseServiceKey', Copy) {
    mustRunAfter copySecretAlloy
    from file('./backend-submodule/firebase')
    include 'service_account_key.json'
    into 'src/main/resources/firebase'
}

compileJava.dependsOn copySecret, copySecretTest, copySecretAlloy, copyFirebaseServiceKey
processResources.dependsOn copySecret, copySecretAlloy, copyFirebaseServiceKey
processTestResources.dependsOn copySecretTest

test {
    useJUnitPlatform()
    dependsOn copySecretTest, copyFirebaseServiceKey
}
